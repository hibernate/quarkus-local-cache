/*
 * SPDX-License-Identifier: Apache-2.0
 * Copyright Red Hat Inc. and Hibernate Authors
 */

@Library('hibernate-jenkins-pipeline-helpers') _

pipeline {
    agent none
    triggers {
        // Run at least once per week, in case of snapshot updates.
        cron '@weekly'
    }
    parameters {
        string(name: 'ORM_REPOSITORY', defaultValue: '', description: 'Git URL to Hibernate ORM repository. If provided, Hibernate ORM will be built locally. Works only in pair with ORM_BRANCH. Provide an http repository URL rather than an ssh one.')
        string(name: 'ORM_BRANCH', defaultValue: '', description: 'Hibernate ORM branch to build from. If provided, Hibernate ORM will be built locally. Works only in pair with ORM_REPOSITORY. Either a pull request ID or a branch name should be provided, but not both at the same time. Use branch if you want to build from a fork repository.')
        string(name: 'ORM_PULL_REQUEST_ID', defaultValue: '', description: 'Hibernate ORM pull request id to build from. If provided, Hibernate ORM will be built locally. Works only in pair with ORM_REPOSITORY. Either a pull request ID or a branch name should be provided, but not both at the same time.')
    }
    options {
        buildDiscarder logRotator(daysToKeepStr: '10', numToKeepStr: '3')
        disableConcurrentBuilds(abortPrevious: true)
        overrideIndexTriggers(false)
    }
    environment {
        TESTCONTAINERS_REUSE_ENABLE = 'true'
    }
    stages {
        stage('Pre-build Hibernate ORM') {
            agent {
                label 'Worker&&Containers'
            }
            post {
                cleanup {
                    dir('hibernate-orm-local-copy') {
                        deleteDir()
                    }
                }
            }
            when {
                beforeAgent true
                expression {
                    return params.ORM_REPOSITORY?.trim() || params.ORM_BRANCH?.trim() || params.ORM_PULL_REQUEST_ID?.trim()
                }
            }
            tools {
                jdk 'OpenJDK 25 Latest'
            }
            steps {
                script {
                    if (params.ORM_BRANCH?.trim() && params.ORM_PULL_REQUEST_ID?.trim()) {
                        error "Both ORM_BRANCH and ORM_PULL_REQUEST_ID are provided. Use only one of these parameters."
                    }
                    if (!params.ORM_REPOSITORY?.trim() || !(params.ORM_BRANCH?.trim() || params.ORM_PULL_REQUEST_ID?.trim())) {
                        error "Both ORM_REPOSITORY and either ORM_BRANCH or ORM_PULL_REQUEST_ID must be not blank if a local build of Hibernate ORM is required. Repository: [${params.ORM_REPOSITORY}], branch: [${params.ORM_BRANCH}, pull request: [${params.ORM_PULL_REQUEST_ID}]]."
                    }
                }
                script {
                    dir('hibernate-orm-local-copy') {
                        // We may get either an http or an ssh repository URLs.
                        // Since this job can work correctly only with an http URL we will try to adapt the ssh url if we spot one:
                        def repositoryUrl = params.ORM_REPOSITORY ==~ /^git@github\.com:.+$/ ? params.ORM_REPOSITORY.replace("git@github.com:", "https://github.com/") : params.ORM_REPOSITORY
                        if (params.ORM_BRANCH?.trim()) {
                            sh "git clone ${repositoryUrl} --depth 1 --branch ${params.ORM_BRANCH} --single-branch ."
                        } else {
                            sh "git clone ${repositoryUrl} --depth 1 --single-branch ."
                            sh "git fetch origin pull/${params.ORM_PULL_REQUEST_ID}/head:orm-branch-to-build"
                            sh "git switch orm-branch-to-build"
                        }

                        sh "./gradlew publishToMavenLocal -x test -Dmaven.repo.local=${env.WORKSPACE_TMP}/.m2repository"
                    }
                    dir(env.WORKSPACE_TMP + '/.m2repository') {
                        stash name: 'orm-local-build-result', includes: "org/hibernate/orm/**"
                    }
                }
            }
        }
        stage('Update dependency and test') {
            agent {
                label 'Worker&&Containers'
            }
            when {
                beforeAgent true
                expression {
                    return params.UPDATE_JOB?.trim() == 'all' || params.UPDATE_JOB?.trim() == env.DEPENDENCY_UPDATE_NAME
                }
            }
            tools {
                jdk 'OpenJDK 25 Latest'
                maven 'Apache Maven 3.9'
            }
            stages {
                stage('Init') {
                    steps {
                        dir(env.WORKSPACE_TMP + '/.m2repository') {
                            script {
                                try {
                                    unstash name: 'orm-local-build-result'
                                } catch (e) {
                                    echo 'Hibernate ORM was not built, ignoring unstash of snapshot ORM jars'
                                }
                            }
                        }
                    }
                }
                stage('Update dependency') {
                    steps {
                        script {
                            def ormUpdateRulesFile = sh(script: "echo \"file://\$(pwd)/ci/dependency-update/rules-orm.xml\"", returnStdout: true).trim()
                            withMaven(mavenLocalRepo: env.WORKSPACE_TMP + '/.m2repository') {
                                sh "./mvnw --quiet -U -Pdependency-update org.codehaus.mojo:versions-maven-plugin:update-properties -DgenerateBackupPoms=false -DallowMajorUpdates=true -DallowMinorUpdates=true -DallowSnapshots=true -DallowDowngrade=true -DincludeProperties='version.hibernate' -Dmaven.version.rules='${ormUpdateRulesFile}'"
                            }
                            echo "Updated version properties:"
                            sh "git --no-pager diff HEAD"
                        }
                    }
                }
                stage('Test') {
                    options {
                        timeout(time: 30, unit: 'MINUTES')
                    }
                    steps {
                        withMaven(mavenLocalRepo: env.WORKSPACE_TMP + '/.m2repository') {
                            sh "mvn clean install -U -Pdependency-update"
                        }
                    }
                }
            }
        }
    }
    post {
        always {
            notifyBuildResult maintainers: 'marko@hibernate.org'
        }
    }
}
